import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'com.cinnober.gradle.semver-git'

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.lasthopesoftware.bluewater"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode getGeneratedVersionCode()
        versionName project.version
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            java {
                exclude '**/specs/**'
            }
        }

        test {
            java {
                setRoot 'src/main'
                include '**/specs/**'
            }
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

project.ext.set("archivesBaseName", "project-blue-" + project.version)

def getGeneratedVersionCode() {
    // current dir is <your proj>/app, so it's likely that all your git repo files are in the dir
    // above.
    ext.repo = Grgit.open(project.file('..'))

    // should result in the same value as running
    // git rev-list <checked out branch name> | wc -l
    def initialVersionCode = 3761 // the initial version code before tags were used to increment version code
    def numberOfReleases = ext.repo.tag.list().size()
    return numberOfReleases + initialVersionCode
}

dependencies {
    implementation project(':pagerslidingtabstrip')
    implementation project(':futures')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'commons-io:commons-io:2.4'
    implementation('com.github.worker8:tourguide:1.0.19-SNAPSHOT@aar') {
        transitive = true
    }
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }
    implementation 'org.slf4j:slf4j-api:1.7.12'
    implementation 'com.annimon:stream:1.1.9'
    implementation 'com.namehillsoftware:handoff:0.12.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'
    implementation 'com.namehillsoftware:lazy-j:0.11.0'
    implementation 'com.github.danrien:xmlwise:master-SNAPSHOT'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.8.2'
    implementation 'com.google.android.exoplayer:extension-okhttp:2.8.2'
    implementation 'com.squareup.okio:okio:1.14.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.github.namehillsoftware:artful:0.2.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'commons-codec:commons-codec:1.11'
}