import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'com.cinnober.gradle.semver-git'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.lasthopesoftware.bluewater"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode getGeneratedVersionCode()
        versionName project.version
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            java {
                exclude '**/specs/**'
            }
        }

        test {
            java {
                setRoot 'src/main'
                include '**/specs/**'
            }
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
}

project.ext.set("archivesBaseName", "music-canoe-" + project.version)

def getGeneratedVersionCode() {
    // current dir is <your proj>/app, so it's likely that all your git repo files are in the dir
    // above.
    ext.repo = Grgit.open(project.file('..'))

    // should result in the same value as running
    // git rev-list <checked out branch name> | wc -l
    def initialVersionCode = 3761 // the initial version code before tags were used to increment version code
    def numberOfReleases = ext.repo.tag.list().size()
    return numberOfReleases + initialVersionCode
}

dependencies {
    compile project(':pagerslidingtabstrip')
    compile project(':futures')
    compile project(':objective-droid')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'org.assertj:assertj-core:3.+'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'joda-time:joda-time:2.9.9'
    compile 'commons-io:commons-io:2.4'
    compile 'com.github.amlcurran.showcaseview:library:5.4.3'
    compile 'com.github.tony19:logback-android-core:1.1.1-6'
    compile('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.annimon:stream:1.1.9'
    compile 'com.github.danrien:handoff:0.3.0'
    compile 'io.reactivex.rxjava2:rxjava:2.1.5'
    compile 'com.github.danrien:lazy-j:feature~rename-ilazy-SNAPSHOT'
    compile 'com.github.danrien:xmlwise:master-SNAPSHOT'
    compile 'com.android.support:multidex:1.0.2'
}