import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.cinnober.gradle.semver-git'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        applicationId "com.lasthopesoftware.bluewater"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode getGeneratedVersionCode()
        versionName project.version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            java {
                exclude '**/specs/**'
            }
        }

        test {
            java {
                setRoot 'src/main'
                include '**/specs/**'
            }
        }
    }
}

project.ext.set("archivesBaseName", "music-canoe-" + project.version);

def getGeneratedVersionCode() {
    // current dir is <your proj>/app, so it's likely that all your git repo files are in the dir
    // above.
    ext.repo = Grgit.open(project.file('..'))

    // should result in the same value as running
    // git rev-list <checked out branch name> | wc -l
    def initialVersionCode = 3761 // the initial version code before tags were used to increment version code
    def numberOfReleases = ext.repo.tag.list().size()
    return numberOfReleases + initialVersionCode
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(':pagerslidingtabstrip')
    compile project(':futures')
    compile project(':objective-droid')
    compile files('libs/xmlwise-debug-1_2.jar')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'org.assertj:assertj-core:2.+'
    compile 'com.android.support:appcompat-v7:26.0.1'
    compile 'com.android.support:design:26.0.1'
    compile 'joda-time:joda-time:2.4'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-validator:commons-validator:1.4.0'
    compile 'com.github.amlcurran.showcaseview:library:5.0.0'
    compile 'com.github.tony19:logback-android-core:1.1.1-3'
    compile 'com.github.tony19:logback-android-classic:1.1.1-3'
    compile 'org.slf4j:slf4j-api:1.7.6'
    compile 'com.android.support:support-v4:26.0.1'
    compile 'com.annimon:stream:1.1.9'
    compile project(path: ':messenger')
    compile 'io.reactivex.rxjava2:rxjava:2.1.5'
    compile 'com.github.danrien:lazy-j:660f29234d585fa1bc81115fb81ff1b1e273687e'
}