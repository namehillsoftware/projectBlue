import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id "de.mannodermaus.android-junit5" version "1.9.3.0"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-parcelize'

def androidKeyStorePassword = hasProperty('androidKeyStorePassword') ? androidKeyStorePassword : null
def hasKeyStorePassword = { androidKeyStorePassword != null && !androidKeyStorePassword.isEmpty() }
if (!hasKeyStorePassword()) {
	androidKeyStorePassword = System.getenv('ANDROID_KEY_STORE_PASSWORD')
}

def androidStorePassword = hasProperty('androidStorePassword') ? androidStorePassword : ""
def hasAndroidStorePassword = { androidStorePassword != null && !androidStorePassword.isEmpty() }
if (!hasAndroidStorePassword()) {
	androidStorePassword = System.getenv('ANDROID_STORE_PASSWORD')
}

def isSignedExternally = { hasKeyStorePassword() && hasAndroidStorePassword() }

def compose_compiler_version = '1.5.3'

android {
	compileSdk 34

	defaultConfig {
        applicationId "com.lasthopesoftware.bluewater"
        minSdkVersion 21
        targetSdkVersion 34
		versionName gitVersion()
		archivesBaseName = "project-blue-" + gitVersion()
	}

	signingConfigs {
		debug {
			// You need to specify either an absolute path or include the
			// keystore file in the same directory as the build.gradle file.
			storeFile file("androidstore")
			storePassword androidStorePassword
			keyAlias "store"
			keyPassword androidKeyStorePassword
		}
		release {
			// You need to specify either an absolute path or include the
			// keystore file in the same directory as the build.gradle file.
			storeFile file("androidstore")
			storePassword androidStorePassword
			keyAlias "store"
			keyPassword androidKeyStorePassword
		}
	}

	buildFeatures {
		compose = true
	}

	compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

	kotlinOptions {
		jvmTarget = '11'
	}

	buildTypes {
        debug {
            minifyEnabled false
			signingConfig = (isSignedExternally() ? signingConfigs.debug : signingConfig)
		}
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig (isSignedExternally() ? signingConfigs.release : signingConfig)
        }
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/INDEX.LIST']
			resources {
				excludes += '/META-INF/{AL2.0,LGPL2.1}'
			}
		}
    }

    defaultConfig {

		versionCode getGeneratedVersionCode()
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		vectorDrawables {
			useSupportLibrary true
		}
	}

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
			all {
				maxHeapSize = "2048m"
				maxParallelForks = 3
			}
		}
	}

    lint {
        abortOnError false
    }

	composeOptions {
		kotlinCompilerExtensionVersion compose_compiler_version
	}
	namespace 'com.lasthopesoftware.bluewater'
}

tasks.withType(KotlinCompile).configureEach {
	compilerOptions {
		freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn")
		freeCompilerArgs.add("-Xcontext-receivers")
	}
}

def gitVersion() {
	try {
		def gitDescribe = providers.exec {
			it.ignoreExitValue = true
			commandLine("git", "describe")
		}.standardOutput.asText.get().trim()

		def latestTag = providers.exec {
			it.ignoreExitValue = true
			commandLine("git", "describe", "--abbrev=0")
		}.standardOutput.asText.get().trim()

		if (gitDescribe != latestTag) {
			return latestTag + "-SNAPSHOT"
		}

		return gitDescribe
	} catch (ignored) {
		return "0.0.1"
	}
}

def getGeneratedVersionCode() {
    // should result in the same value as running
    // git rev-list <checked out branch name> | wc -l
    def initialVersionCode = 3761 // the initial version code before tags were used to increment version code

	try {
		def numberOfReleases = providers.exec {
			it.ignoreExitValue = true
			commandLine("sh", "-c", "git tag | wc -l")
		}.standardOutput.asText.get() as Integer
		return numberOfReleases + initialVersionCode
	} catch (ignored) {
		return initialVersionCode
	}
}

dependencies {
	def compose_version = '1.5.1'
	def media3_version = '1.1.1'
	def lifecycle_version = '2.6.2'
	def junit5_version = '5.10.0'

	implementation 'androidx.core:core-ktx:1.12.0'
	implementation 'androidx.annotation:annotation:1.7.0'
	implementation 'androidx.work:work-runtime:2.8.1'
	implementation 'androidx.media:media:1.6.0'
	implementation 'androidx.palette:palette-ktx:1.0.0'
	implementation 'androidx.preference:preference-ktx:1.2.1'
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
	implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
	implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
	implementation 'com.google.android.material:material:1.9.0'
	implementation 'com.google.guava:guava:31.1-android'
	implementation 'joda-time:joda-time:2.12.5'
	implementation 'commons-io:commons-io:2.11.0'
	implementation 'org.slf4j:slf4j-api:2.0.9'
	implementation 'com.github.tony19:logback-android:3.0.0'
	implementation 'com.namehillsoftware:handoff:0.19.6'
	implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
	implementation 'com.namehillsoftware:lazy-j:0.11.0'
	implementation 'com.github.danrien:xmlwise:-SNAPSHOT'
	implementation "androidx.media3:media3-exoplayer:$media3_version"
	implementation "androidx.media3:media3-datasource-okhttp:$media3_version"
	implementation 'com.squareup.okio:okio:3.5.0'
	implementation 'com.squareup.okhttp3:okhttp:4.11.0'
	implementation 'com.namehillsoftware:querydroid:0.5.0'
	implementation "androidx.compose.ui:ui:$compose_version"
	implementation "androidx.compose.material:material:$compose_version"
	implementation "androidx.compose.ui:ui-tooling:$compose_version"
	implementation "androidx.compose.runtime:runtime-rxjava2:$compose_version"
	implementation 'com.google.accompanist:accompanist-systemuicontroller:0.27.0'
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
	implementation 'androidx.activity:activity-compose:1.7.2'
	implementation "androidx.compose.compiler:compiler:$compose_compiler_version"
	implementation 'dev.olshevski.navigation:reimagined:1.5.0'
	debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
	testImplementation 'commons-codec:commons-codec:1.15'
	testImplementation 'junit:junit:4.13.2'
	testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5_version"
	testImplementation 'org.assertj:assertj-core:3.24.2'
	testImplementation 'org.robolectric:robolectric:4.9'
	testImplementation 'androidx.test.ext:junit-ktx:1.1.5'
	testImplementation 'androidx.test:core:1.5.0'
	testImplementation 'io.mockk:mockk:1.13.7'
	testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5_version"
	testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit5_version"
	androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}
