import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	alias(libs.plugins.androidJUnit5)
	alias(libs.plugins.kotlinSerialization)
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

def androidKeyStorePassword = hasProperty('androidKeyStorePassword') ? androidKeyStorePassword : null
def hasKeyStorePassword = { androidKeyStorePassword != null && !androidKeyStorePassword.isEmpty() }
if (!hasKeyStorePassword()) {
	androidKeyStorePassword = System.getenv('ANDROID_KEY_STORE_PASSWORD')
}

def androidStorePassword = hasProperty('androidStorePassword') ? androidStorePassword : ""
def hasAndroidStorePassword = { androidStorePassword != null && !androidStorePassword.isEmpty() }
if (!hasAndroidStorePassword()) {
	androidStorePassword = System.getenv('ANDROID_STORE_PASSWORD')
}

def isSignedExternally = { hasKeyStorePassword() && hasAndroidStorePassword() }

def copyDependencyList = tasks.register("copyDependencyList", Copy) {
	into "$temporaryDir/raw" from "$project.rootDir/acknowledgements.md"

	outputs.dir(temporaryDir)
}

preBuild.dependsOn(copyDependencyList)

android {
    compileSdk 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
		versionName gitVersion()
		base.archivesName = "project-blue-" + gitVersion()
	}

	signingConfigs {
		debug {
			// You need to specify either an absolute path or include the
			// keystore file in the same directory as the build.gradle file.
			storeFile file("androidstore")
			storePassword androidStorePassword
			keyAlias "store"
			keyPassword androidKeyStorePassword
		}
		release {
			// You need to specify either an absolute path or include the
			// keystore file in the same directory as the build.gradle file.
			storeFile file("androidstore")
			storePassword androidStorePassword
			keyAlias "store"
			keyPassword androidKeyStorePassword
		}
	}

	buildFeatures {
		compose = true
		buildConfig = true
	}

	compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11

		coreLibraryDesugaringEnabled = true
	}

	kotlinOptions {
		jvmTarget = '11'
	}

	sourceSets {
		main {
			res.srcDirs += copyDependencyList.map { it.temporaryDir }
		}
	}

	buildTypes {
        debug {
            minifyEnabled false
			signingConfig = (isSignedExternally() ? signingConfigs.debug : signingConfig)
		}
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig (isSignedExternally() ? signingConfigs.release : signingConfig)
			ndk {
				debugSymbolLevel = 'SYMBOL_TABLE'
			}
        }
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/INDEX.LIST']
			resources {
				excludes += '/META-INF/{AL2.0,LGPL2.1}'
			}
		}
    }

    defaultConfig {

		versionCode getGeneratedVersionCode()
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		vectorDrawables {
			useSupportLibrary true
		}
	}

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
			all {
				maxHeapSize = "2048m"
				maxParallelForks = 3
			}
		}
	}

    lint {
        abortOnError false
    }

	namespace 'com.lasthopesoftware.bluewater'
}

tasks.withType(KotlinCompile).configureEach {
	compilerOptions {
		freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn")
	}
}

def gitVersion() {
	try {
		def gitDescribe = providers.exec {
			it.ignoreExitValue = true
			commandLine("git", "describe")
		}.standardOutput.asText.get().trim()

		def latestTag = providers.exec {
			it.ignoreExitValue = true
			commandLine("git", "describe", "--abbrev=0")
		}.standardOutput.asText.get().trim()

		if (gitDescribe != latestTag) {
			return latestTag + "-SNAPSHOT"
		}

		return gitDescribe
	} catch (ignored) {
		return "0.0.1"
	}
}

def getGeneratedVersionCode() {
    // should result in the same value as running
    // git rev-list <checked out branch name> | wc -l
    def initialVersionCode = 3761 // the initial version code before tags were used to increment version code

	try {
		def numberOfReleases = providers.exec {
			it.ignoreExitValue = true
			commandLine("sh", "-c", "git tag | wc -l")
		}.standardOutput.asText.get() as Integer
		return numberOfReleases + initialVersionCode
	} catch (ignored) {
		return initialVersionCode
	}
}

dependencies {
	coreLibraryDesugaring libs.desugar.jdk.libs

	debugImplementation libs.androidx.compose.ui.test.manifest
	implementation libs.androidx.core.ktx
	implementation libs.androidx.annotation
	implementation libs.androidx.work.runtime
	implementation libs.androidx.media
	implementation libs.androidx.palette.ktx
	implementation libs.androidx.preference.ktx
	implementation libs.androidx.lifecycle.viewmodel.ktx
	implementation libs.androidx.lifecycle.process
	implementation libs.androidx.lifecycle.service
	implementation libs.androidx.lifecycle.runtime.ktx
	implementation libs.androidx.media3.exoplayer
	implementation libs.androidx.startup.runtime
	implementation libs.androidx.compose.ui
	implementation libs.androidx.compose.material
	implementation libs.androidx.compose.runtime.rxjava3
	implementation libs.androidx.activity.compose
	implementation libs.markdown.renderer
	implementation libs.markdown.renderer.m2
	implementation libs.material
	implementation libs.guava
	implementation libs.joda.time
	implementation libs.commons.io
	implementation libs.slf4j.api
	implementation libs.ktor.client
	implementation libs.ktor.core
	implementation libs.logback.android
	implementation libs.handoff
	implementation libs.rxjava
	implementation libs.lazy.j
	implementation libs.jsoup
	implementation libs.okio
	implementation libs.okhttp
	implementation libs.querydroid
	implementation libs.reimagined
	implementation libs.gson
	implementation libs.kotlinx.coroutines.rx3
	implementation libs.androidx.compose.ui.tooling
	testCompileOnly libs.junit
	testImplementation libs.androidx.junit.ktx
	testImplementation libs.androidx.test.core
	testImplementation libs.androidx.test.runner
	testImplementation libs.commons.codec
	testImplementation libs.junit.jupiter.api
	testImplementation libs.assertj.core
	testImplementation libs.robolectric
	testImplementation libs.mockk
	testImplementation libs.kotlinx.coroutines.test
	testImplementation libs.androidx.compose.ui.test.junit4
	testRuntimeOnly libs.junit.platform.launcher
	testRuntimeOnly libs.junit.vintage.engine
}
