import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'com.cinnober.gradle.semver-git'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

def androidKeyStorePassword = hasProperty('androidKeyStorePassword') ? androidKeyStorePassword : null
if (androidKeyStorePassword == null || androidKeyStorePassword.isEmpty()) {
	androidKeyStorePassword = System.getenv('ANDROID_KEY_STORE_PASSWORD')
}

def androidStorePassword = hasProperty('androidStorePassword') ? androidStorePassword : null
if (androidStorePassword == null || androidStorePassword.isEmpty()) {
	androidStorePassword = System.getenv('ANDROID_STORE_PASSWORD')
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "com.lasthopesoftware.bluewater"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode getGeneratedVersionCode()
        versionName project.version
		multiDexEnabled true
	}

	signingConfigs {
        release {
            // You need to specify either an absolute path or include the
            // keystore file in the same directory as the build.gradle file.
            storeFile file("androidstore")
            storePassword androidStorePassword
            keyAlias "store"
            keyPassword androidKeyStorePassword
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
		}
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            java {
                exclude '**/specs/**'
            }
        }

        test {
            java {
                setRoot 'src/main'
                include '**/specs/**'
            }
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
			all {
				maxHeapSize = "1024m"
			}
		}
	}
}

project.ext.set("archivesBaseName", "project-blue-" + project.version)

def getGeneratedVersionCode() {
    // current dir is <your proj>/app, so it's likely that all your git repo files are in the dir
    // above.
    ext.repo = Grgit.open(project.file('..'))

    // should result in the same value as running
    // git rev-list <checked out branch name> | wc -l
    def initialVersionCode = 3761 // the initial version code before tags were used to increment version code
    def numberOfReleases = ext.repo.tag.list().size()
    return numberOfReleases + initialVersionCode
}

dependencies {
    implementation project(':futures')
    implementation 'androidx.annotation:annotation:1.1.0'
	implementation 'androidx.multidex:multidex:2.0.1'
	implementation 'androidx.work:work-runtime:2.4.0'
	implementation "androidx.media:media:1.1.0"
	implementation "androidx.preference:preference-ktx:1.1.1"
	implementation 'com.google.android.material:material:1.2.1'
	implementation 'androidx.legacy:legacy-support-v4:1.0.0'
	implementation 'com.android.support:multidex:1.0.3'
	implementation 'androidx.core:core-ktx:1.3.1'
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
	implementation 'com.google.guava:guava:28.2-android'
	implementation 'joda-time:joda-time:2.10.6'
	implementation 'commons-io:commons-io:2.4'
	implementation('com.github.worker8:tourguide:1.0.19-SNAPSHOT@aar') {
		transitive = true
	}
	implementation 'com.github.tony19:logback-android-core:1.1.1-6'
	implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
		// workaround issue #73
		exclude group: 'com.google.android', module: 'android'
	}
	implementation 'org.slf4j:slf4j-api:1.7.30'
	implementation 'com.namehillsoftware:handoff:0.18.0'
	implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
	implementation 'com.namehillsoftware:lazy-j:0.11.0'
	implementation 'com.github.danrien:xmlwise:master-SNAPSHOT'
	implementation 'com.google.android.exoplayer:exoplayer-core:2.11.8'
	implementation 'com.google.android.exoplayer:extension-okhttp:2.11.8'
	implementation 'com.squareup.okio:okio:2.7.0'
	implementation 'com.squareup.okhttp3:okhttp:4.8.0'
	implementation 'com.github.namehillsoftware:artful:0.3.0'
	testImplementation 'com.annimon:stream:1.2.1'
	testImplementation 'junit:junit:4.13'
	testImplementation 'org.mockito:mockito-core:3.4.4'
	testImplementation 'org.assertj:assertj-core:3.16.1'
	testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'commons-codec:commons-codec:1.11'
    testImplementation 'androidx.test:core:1.3.0'
}
